## 存储控制器 (2017.12.08)
## 存储控制器作用
![存储控制器作用](https://github.com/GalenDeng/Embedded-Linux/blob/master/9.%20%E5%AD%98%E5%82%A8%E6%8E%A7%E5%88%B6%E5%99%A8/%E5%AD%98%E5%82%A8%E6%8E%A7%E5%88%B6%E5%99%A8%E4%BD%9C%E7%94%A8.JPG)
* cpu不管外设是啥东西，它是通过存储管理器和外设(如:SDRAM,网卡DM9000)进行沟通的,存储管理
* 器根据配置文件知道外设的各个地址,进而分辨他们,这时外设应该：发送片选信号 => 发送bank选择信号 => 发送列地址 => 发送行地址 => ...  
## SDRAM的结构图
* bank0,bank1这些我们可以理解为不同的块
* bank => 行地址(row) => 列地址(column)
![SDRAM的结构图](https://github.com/GalenDeng/Embedded-Linux/blob/master/9.%20%E5%AD%98%E5%82%A8%E6%8E%A7%E5%88%B6%E5%99%A8/SDRAM%E7%9A%84%E7%BB%93%E6%9E%84%E5%9B%BE.JPG)
## 访问一个芯片需要的条件
![访问一个芯片需要的条件](https://github.com/GalenDeng/Embedded-Linux/blob/master/9.%20%E5%AD%98%E5%82%A8%E6%8E%A7%E5%88%B6%E5%99%A8/%E8%AE%BF%E9%97%AE%E4%B8%80%E4%B8%AA%E8%8A%AF%E7%89%87%E9%9C%80%E8%A6%81%E7%9A%84%E6%9D%A1%E4%BB%B6.JPG)
* 地址线、数据线、时钟/频率、相关芯片信息、刷新周期、数据位宽
* 位宽 ： 看原理图
* 刷新周期、行地址、列地址、bank : 看内存芯片手册
* 从 S3C2440的芯片手册查看,确定使用哪个port来和内存芯片(K4S561632N)的bank连接
## s3c2440的cpu和内存芯片K4S561632N的连接方式
![s3c2440和内存芯片的连接方式](https://github.com/GalenDeng/Embedded-Linux/blob/master/9.%20%E5%AD%98%E5%82%A8%E6%8E%A7%E5%88%B6%E5%99%A8/s3c2440%E5%92%8C%E5%86%85%E5%AD%98%E8%8A%AF%E7%89%87%E7%9A%84%E8%BF%9E%E6%8E%A5%E6%96%B9%E5%BC%8F.JPG)
* 相对应的s3c2440芯片手册介绍
* ![s3c2440的内存地址总线连接方式](https://github.com/GalenDeng/Embedded-Linux/blob/master/9.%20%E5%AD%98%E5%82%A8%E6%8E%A7%E5%88%B6%E5%99%A8/s3c2440%E7%9A%84%E5%86%85%E5%AD%98%E5%9C%B0%E5%9D%80%E6%80%BB%E7%BA%BF%E8%BF%9E%E6%8E%A5%E6%96%B9%E5%BC%8F.JPG)
* 网卡和nor flash 术语上叫做 RAM like
* SRAM : 贵，操作简单，直接读写 ; SDRAM : 需要 bank地址、行地址、列地址、刷新频率
* 网卡，nor flash 可以用bank0-bank5 ; SDRAM  bank6-bank7

## head.S (SDRAM实验)
```
@*************************************************************************
@ File：head.S
@ 功能：设置SDRAM，将程序复制到SDRAM，然后跳到SDRAM继续执行
@*************************************************************************       

.equ        MEM_CTL_BASE,       0x48000000
.equ        SDRAM_BASE,         0x30000000

.text
.global _start
_start:
    bl  disable_watch_dog               @ 关闭WATCHDOG，否则CPU会不断重启
    bl  memsetup                        @ 设置存储控制器 // bl 指令会把返回值放在lr寄存器中
    bl  copy_steppingstone_to_sdram     @ 复制代码到SDRAM中  // steppingstone 为片内RAM,它的资源
                                                            // 完全来自于一上电，nand flash把前// 4k的代码复制到片内RAM里面
    ldr pc, =on_sdram                   @ 跳到SDRAM中继续执行
on_sdram:
    ldr sp, =0x34000000                 @ 设置堆栈 我们这里我把堆栈设置成 SDRAM的顶端
    bl  main                            // 调用C函数的之前,必须要设置好堆栈
halt_loop:
    b   halt_loop

disable_watch_dog:
    @ 往WATCHDOG寄存器写0即可
    mov r1,     #0x53000000
    mov r2,     #0x0
    str r2,     [r1]                // 往这个地址写0
    mov pc,     lr      @ 返回      //  bl 指令会把返回值放在lr寄存器里面

copy_steppingstone_to_sdram:
    @ 将Steppingstone的4K数据全部复制到SDRAM中去
    @ Steppingstone起始地址为0x00000000，SDRAM中起始地址为0x30000000
    
    mov r1, #0          // r1 的值为 0
    ldr r2, =SDRAM_BASE
    mov r3, #4*1024     // 4k
1:  
    ldr r4, [r1],#4     @ 从Steppingstone读取4字节的数据，并让源地址加4 [r1] :内存单位为 0 地址(地                     @ 址为r2)
    str r4, [r2],#4     @ 将此4字节的数据复制到SDRAM中，并让目地地址加4
    cmp r1, r3          @ 判断是否完成：源地址等于Steppingstone的未地址？
    bne 1b              @ 若没有复制完，继续
    mov pc,     lr      @ 返回

memsetup:
    @ 设置存储控制器以便使用SDRAM等外设

    mov r1,     #MEM_CTL_BASE       @ 存储控制器的13个寄存器的开始地址  // 0x48000024
    adrl    r2, mem_cfg_val         @ 这13个值的起始存储地址  // 13个寄存器地址放在r2,一个寄存器地址是4个字节
    add r3,     r1, #52             @ 13*4 = 52             // +偏移地址
1:                                                                  // A
    ldr r4,     [r2], #4            @ 读取设置值，并让r2加4
    str r4,     [r1], #4            @ 将此值写入寄存器，并让r1加4 //str : storage 存储
    cmp r1,     r3                  @ 判断是否设置完所有13个寄存器
    bne 1b                          @ 若没有写成，继续  // r1 != r3时,往A跳转继续循环该局部标号标记的函数
    mov pc,     lr                  @ 返回   // 通过lr寄存器存储的地址，将该地址返回给pc,即返回到                                           // bl  copy_steppingstone_to_sdram 该函数执行


.align 4
mem_cfg_val:
    @ 存储控制器13个寄存器的设置值
    .long   0x22011110      @ BWSCON
    .long   0x00000700      @ BANKCON0
    .long   0x00000700      @ BANKCON1
    .long   0x00000700      @ BANKCON2
    .long   0x00000700      @ BANKCON3  
    .long   0x00000700      @ BANKCON4
    .long   0x00000700      @ BANKCON5
    .long   0x00018005      @ BANKCON6
    .long   0x00018005      @ BANKCON7
    .long   0x008C07A3      @ REFRESH
    .long   0x000000B1      @ BANKSIZE
    .long   0x00000030      @ MRSRB6
    .long   0x00000030      @ MRSRB7
```
## s3c2440的程序启动方式
![s3c2440的程序启动方式](https://github.com/GalenDeng/Embedded-Linux/blob/master/9.%20%E5%AD%98%E5%82%A8%E6%8E%A7%E5%88%B6%E5%99%A8/s3c2440%E7%9A%84%E7%A8%8B%E5%BA%8F%E5%90%AF%E5%8A%A8%E6%96%B9%E5%BC%8F.JPG)
* 启动方式对比
```
nor flash启动 ： s3c2440的cpu的0地址指向nor flash 的bank0 
nand flash启动： s3c2440的cpu的0地址指向片内RAM(SRAM)[steppingstone],nand flash 把前4k的代码复制到片内RAM,从0地址开始执行程序
```
* 链接地址 意义 : 运行时，程序应该位于哪里
* head.S => 从片内RAM的代码复制到SDRAM中(0x30000000)

## 反汇编指令
```
galen@HD66:/work/nfs_root/hardware/sdram$ cat sdram.dis 

sdram_elf:     file format elf32-littlearm

Disassembly of section .text:
 链接地址     机器码
30000000 <_start>:                                          // 程序开始执行的地址
30000000:	eb000005 	bl	3000001c <disable_watch_dog>
30000004:	eb000010 	bl	3000004c <memsetup>
30000008:	eb000007 	bl	3000002c <copy_steppingstone_to_sdram>
3000000c:	e59ff090 	ldr	pc, [pc, #144]	; 300000a4 <mem_cfg_val+0x34>
// arm里面 pc = 当前指令的地址 + 8 当前指令的地址: 0x0c
// 所以 ldr	pc, [pc, #144] 的意义是 : pc = pc + #144 = 0xc + 8 + 144 = 164 = 0xa4 ;
// 即从0xa4里面取出一个值赋给pc
// 该指令执行完后 pc = 30000010    // 跳到SDRAM里面去
// 和 30000010 <on_sdram>: 相对应

30000010 <on_sdram>:
30000010:	e3a0d30d 	mov	sp, #872415232	; 0x34000000
30000014:	eb000032 	bl	300000e4 <main>

30000018 <halt_loop>:
30000018:	eafffffe 	b	30000018 <halt_loop>

3000001c <disable_watch_dog>:
3000001c:	e3a01453 	mov	r1, #1392508928	; 0x53000000
30000020:	e3a02000 	mov	r2, #0	; 0x0
30000024:	e5812000 	str	r2, [r1]
30000028:	e1a0f00e 	mov	pc, lr

3000002c <copy_steppingstone_to_sdram>:
3000002c:	e3a01000 	mov	r1, #0	; 0x0
30000030:	e3a02203 	mov	r2, #805306368	; 0x30000000
30000034:	e3a03a01 	mov	r3, #4096	; 0x1000
30000038:	e4914004 	ldr	r4, [r1], #4
3000003c:	e4824004 	str	r4, [r2], #4
30000040:	e1510003 	cmp	r1, r3
30000044:	1afffffb 	bne	30000038 <copy_steppingstone_to_sdram+0xc>
30000048:	e1a0f00e 	mov	pc, lr

3000004c <memsetup>:
3000004c:	e3a01312 	mov	r1, #1207959552	; 0x48000000
30000050:	e28f2018 	add	r2, pc, #24	; 0x18
30000054:	e1a00000 	nop			(mov r0,r0)
30000058:	e2813034 	add	r3, r1, #52	; 0x34
3000005c:	e4924004 	ldr	r4, [r2], #4
30000060:	e4814004 	str	r4, [r1], #4
30000064:	e1510003 	cmp	r1, r3
30000068:	1afffffb 	bne	3000005c <memsetup+0x10>
3000006c:	e1a0f00e 	mov	pc, lr

30000070 <mem_cfg_val>:
30000070:	22011110 	andcs	r1, r1, #4	; 0x4
30000074:	00000700 	andeq	r0, r0, r0, lsl #14
30000078:	00000700 	andeq	r0, r0, r0, lsl #14
3000007c:	00000700 	andeq	r0, r0, r0, lsl #14
30000080:	00000700 	andeq	r0, r0, r0, lsl #14
30000084:	00000700 	andeq	r0, r0, r0, lsl #14
30000088:	00000700 	andeq	r0, r0, r0, lsl #14
3000008c:	00018005 	andeq	r8, r1, r5
30000090:	00018005 	andeq	r8, r1, r5
30000094:	008c07a3 	addeq	r0, ip, r3, lsr #15
30000098:	000000b1 	streqh	r0, [r0], -r1
3000009c:	00000030 	andeq	r0, r0, r0, lsr r0
300000a0:	00000030 	andeq	r0, r0, r0, lsr r0
300000a4:	30000010 	andcc	r0, r0, r0, lsl r0
300000a8:	e1a00000 	nop			(mov r0,r0)
300000ac:	e1a00000 	nop			(mov r0,r0)
```
## 程序从Steppingstone到SDRAM的执行过程
![程序从Steppingstone到SDRAM的执行过程](https://github.com/GalenDeng/Embedded-Linux/blob/master/9.%20%E5%AD%98%E5%82%A8%E6%8E%A7%E5%88%B6%E5%99%A8/%E7%A8%8B%E5%BA%8F%E4%BB%8ESteppingstone%E5%88%B0SDRAM%E7%9A%84%E6%89%A7%E8%A1%8C%E8%BF%87%E7%A8%8B.JPG)